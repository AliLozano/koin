apply plugin: 'org.jetbrains.kotlin.multiplatform'

archivesBaseName = 'koin-core'
description = 'Koin - simple dependency injection for Kotlin - ' + archivesBaseName
def ideaActive = System.getProperty('idea.active') == 'true'

kotlin {
  jvm()
// Uncomment and refresh gradle for Intellij to "see" native libraries
//    macosX64("nativeCommon")
  macosX64("native")
  if(!ideaActive) {
    iosArm32()
    iosArm64()
    iosX64()
  }


  sourceSets {
    commonMain {
      dependencies {
        api kotlin("stdlib-common")
      }
    }

    commonTest {
      dependencies {
        implementation kotlin("test-common")
        implementation kotlin("test-annotations-common")
        implementation "co.touchlab:testhelp:$testhelp_version"
      }
    }

    jvmMain {
      dependencies {
        api kotlin("stdlib-jdk8")
      }
    }

    jvmTest {
      dependencies {
        implementation kotlin("test-junit")
        implementation "org.mockito:mockito-inline:$mockito_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
        implementation "co.touchlab:stately:$stately_version"
        implementation "co.touchlab:stately-collections:$stately_version"
      }
    }

    nativeMain {
      dependencies {
        implementation "co.touchlab:stately:$stately_version"
        implementation "co.touchlab:stately-collections:$stately_version"
      }
    }

    nativeTest {
      dependencies {
        implementation "co.touchlab:stately:$stately_version"
        implementation "co.touchlab:stately-collections:$stately_version"
      }
    }

    if(!ideaActive) {
      configure([
          targets.iosX64,
          targets.iosArm32,
          targets.iosArm64
      ]) {
        compilations.main.source(sourceSets.nativeMain)
        compilations.test.source(sourceSets.nativeTest)
      }
    }
  }
}

//apply from: '../gradle/publish.gradle'